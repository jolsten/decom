// A bunch of words
start: value*

value: directive
    | include
    | uselist
    | function
    | named_measurand

directive: directive_name "=" WORD
directive_name: "directive_one"i
    | "directive_two"i

include: "INCLUDE" "=" PATH
uselist: NAME "." "USELIST" "=" "{" named_measurand* "}"
function: name "=" NAME "(" ARG* ")"

name: NAME
    | NAME "." NAME
named_measurand: name "=" filter? measurand__start

iterator: "++" INT ("<" INT)?
filter: FILTER_TOK "(" INT iterator? ")"

FILTER_TOK: "mfo"i | "mfv"i
COMMENT_SL: /#[^\r\n]+/
NAME: /[^\s\#\$\;\,\.\(\)\[\]\{\}\<\>]+/ // Not whitespace, period, (), [], {}
ARG:  /[-a-zA-Z0-9,]+/
PATH: /.+/

// imports
%import common.WS
%import common.INT
%import common.WORD
%import .measurand.start -> measurand__start

%ignore WS      // Disregard whitespace in text
%ignore COMMENT_SL // Ignore comments on any single line
