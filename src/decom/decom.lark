// A bunch of words
start: value*

value: include
     | uselist
     | function
     | named_measurand

include: "INCLUDE" "=" PATH
uselist: NAME "." "USELIST" "=" "{" named_measurand* "}"
function: name "=" NAME "(" ARG* ")"

name: NAME
    | NAME "." NAME
named_measurand: name "=" filter? measurand__start

iterator: "++" INT ("<" INT)?
filter: FILTER_TOK "(" INT iterator? ")"

FILTER_TOK: "mfo"i | "mfv"i
NAME:    /[^\s\.\(\)\[\]\{\}]+/ // Not whitespace, period, (), [], {}
ARG:     /[-a-zA-Z0-9,]+/
PATH:    /.+/
COMMENT: /#[^\r\n]+/

// imports
%import common.WS
%import common.INT
%import .calculator.start -> calc__start
%import .measurand.start -> measurand__start

%ignore WS      // Disregard whitespace in text
%ignore COMMENT // Ignore comments on any single line
