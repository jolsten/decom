?start: sum+

?sum: product
    | sum "+" product -> add
    | sum "-" product -> sub

?product: power
    | product "*" power -> mul
    | product "/" power -> div

?power: atom
    | power "**" atom -> pow 

!constant: "PI" | "E" -> constant

number: NUMBER

?atom: PV -> pv
    | number
    | constant
    | "-" atom -> neg
    | "(" sum ")"
    | "float"i "(" sum ")" -> float
    | "fix"i   "(" sum ")" -> fix
    | "round"i "(" sum ")" -> round
    | "floor"i "(" sum ")" -> floor
    | "ceil"i  "(" sum ")" -> ceil
    | "nxtwo"i "(" sum ")" -> nxtwo
    | "sin"i   "(" sum ")" -> sin
    | "cos"i   "(" sum ")" -> cos
    | "tan"i   "(" sum ")" -> tan
    | "asin"i  "(" sum ")" -> asin
    | "acos"i  "(" sum ")" -> acos
    | "atan"i  "(" sum ")" -> atan
    | "atan2"i "(" sum "," sum ")" -> atan2
    | "deg"i   "(" sum ")" -> deg
    | "rad"i   "(" sum ")" -> rad
    | "abs"i   "(" sum ")" -> abs
    | "exp"i   "(" sum ")" -> exp
    | "tento"i "(" sum ")" -> tento
    | "ln"i    "(" sum ")" -> log
    | "log"i   "(" sum ")" -> log10
    | "sqrt"i  "(" sum ")" -> sqrt
    | "max"i   "(" _csv{sum} ")" -> max
    | "min"i   "(" _csv{sum} ")" -> min
    | "if"i    "(" sum "," sum "," sum ")" -> if_
    | "hamdist"i "(" sum "," sum ")" -> hamdist

_csv{x}: x ("," x)*

PV: "PV"
// Force INT priority over FLOAT
INTEGER.1: INT
FLOAT.0: NUMBER


%import common.WS_INLINE
%import common.INT
%import common.NUMBER

%ignore WS_INLINE
