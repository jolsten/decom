?start: parameter

?iterator: up_iterator | dn_iterator
up_iterator: "++" int ("<" int)?
dn_iterator: "--" int ("<" int)?

parameter: "[" all_fragments "]"
    | "[" all_fragments "]" bit_op                  -> parameter_with_bitop
    | "[" "(" all_fragments ")" iterator "]" bit_op -> generator_parameter_with_bitop
    | "["     all_fragments     iterator "]" bit_op -> generator_parameter_with_bitop
    | "[" "(" all_fragments ")" iterator "]"        -> generator_parameter
    | "["     all_fragments     iterator "]"        -> generator_parameter
    | "[" all_fragments "]" bit_op iterator         -> supercom_parameter_with_bitop
    | "[" all_fragments "]" iterator                -> supercom_parameter

!bit_op: ("AND" | "OR" | "XOR") const_value

all_fragments: cr_fragments ("+" cr_fragments)* -> concatenate

!cr_fragments: "~"? (fragments | constant) "R"?

constant: ("x" HEX | "o" OCT | "b" BIN)
    | "'" ("x" HEX | "o" OCT | "b" BIN) "'"

fragments: int                  -> fragments_single
    | int "-" int               -> fragments_range
    | int "-" int ":" bit_spec  -> fragments_bits_last
    | int ":" bit_spec          -> fragments_bits_last
    | int ":" bit_spec "-" int  -> fragments_bits_first

?bit_spec: bit_list
    | bit_mask
bit_list:  word_spec ("," word_spec)* -> concatenate

?word_spec: range
range: int "-" int
    | int

?const_value: "x" hex | "o" oct | "b" bin
bit_mask: const_value

hex: HEX -> hex2dec
oct: OCT -> oct2dec
bin: BIN -> bin2dec
int: INT -> integer

HEX: /[0-9a-fA-F]+/
OCT: /[0-7]+/
BIN: /[01]+/

%import common (INT, WS)
%ignore WS

// [1:5-8] == [1:8-5] == [1:5,6,7,8] == [1:8,7,6,5]
// [1:1,3,5,7] == [1:7,5,3,1] == [1:7+1:5+1:3+1:1] != [1:1+1:3+1:5+1:7]
// [1:4-1-4] == [1-4:1-4] == [1:1-4+2:1-4+3:1-4+4:1-4]

// [1:8,5-3,1-4] -> parser ambiguity
// [1-4:8,5-3,1] -> parsed correctly
// ^^^^^^^^^^^^^ == [1:b10011101-4] == [1-4:b10011101] 

// [1] == [1:1-8] == [1:8-1] == [1:1,2,3,4,5,6,7,8] == [1:8,7,6,5,4,3,2,1] == [1:b11111111] == [1:o377] == [1:xff] == [1:xFF]

// [1-4:xf0] == [1:xf0-4]
